#compdef docker-volumes

# Docker-volumes autocompletion for oh-my-zsh
# Requires: Docker Volumes installed
# Author: Dmitry Fedorv (@dmajere)

# ----- Helper functions
#
# Output a list of all existing docker volume
#
__docker_volumes() {
    declare -a vol_cmd
    vol_cmd=($(docker-volumes list | awk 'NR>2{print $1":[PATH("$NF")]"}'))
    _describe 'volumes' vol_cmd
}

# ----- Commands
# Seperate function for each command, makes extension easier later
# ---------------------------

__list() {
    _arguments \
        '(-q,--quiet)'{-q,--quiet}'[Display only IDs]'
}

__inspect() {
   __docker_volumes
}

__rm() {
    __docker_volumes
}

__export() {
    _arguments \
        '(-p,--pause)'{-p,--pause}'[Pause any container using the volume before export]'
    __docker_volumes
}

__import() {
    _arguments '*:files:_files'
}

# end commands ---------
# ----------------------

local -a _1st_arguments
_1st_arguments=(
    "list":"List all volumes"
    "inspect":"Get details of volume"
    "rm":"Delete a volume"
    "export":"Export a as a tarball. Prints to stdout"
    "import":"Import a tarball produced by the export command the specified container"
    "help":"Shows a list of commands or help for one command"
)

_arguments '*:: :->command'

if (( CURRENT == 1 )); then
    _describe -t commands "docker-volumes command" _1st_arguments
    return
fi

local -a _command_args
case "$words[1]" in
    list)
       __list ;;
    inspect)
        __inspect ;;
    rm)
        __rm ;;
    export)
        __export ;;
    import)
        __import ;;
esac
